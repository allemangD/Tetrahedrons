#version 430

layout(std430, binding=0) buffer TetraVerts
{
    vec4 tetraVerts[];
};

layout(std430, binding=1) buffer TetraInds
{
    uint tetraInds[];
};

layout(std430, binding=2) buffer PolyVerts
{
    vec4 polyVerts[];
};

layout(std430, binding=3) buffer PolyEdges
{
    uint polyEdges[];
};

uniform PlaneMats
{
    mat4 tform;
};

layout(local_size_x = 128, local_size_y = 1, local_size_z = 1) in;

void main () {
    int ti0 = int(gl_GlobalInvocationID.x * 4);
    int pv0 = int(gl_GlobalInvocationID.x * 4);
    int pe0 = int(gl_GlobalInvocationID.x * 8);
    
    int ii = 0;
    int vv = 0;
    
    for (int i =     0; i < 4; i++)
    for (int j = i + 1; j < 4; j++)
    {
        vec4 a = tetraVerts[tetraInds[ti0 + i]];
        vec4 a_ = tform * a;
        
        vec4 b = tetraVerts[tetraInds[ti0 + j]];
        vec4 b_ = tform * b;

        if (a_.x * b_.x < 0)
        {
            vec4 p = (b-a) / (a_.x - b_.x) * b_.x + b;
            polyVerts[pv0 + vv++] = p;
        }
    }
    
    for (int i = 0; i < vv; i++)
    {
        polyEdges[pe0 + ii++] = pv0 + i;
        polyEdges[pe0 + ii++] = pv0 + (i + 1) % vv;
    }
    
    while (vv < 4) polyVerts[pv0 + vv++] = vec4(0);
    while (ii < 8) polyEdges[pe0 + ii++] = 0;
}